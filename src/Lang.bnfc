Program. Program ::= [Def] ;

terminator nonempty Def ";" ;

DSig. Def ::= Sig ;
DBind. Def ::= Bind ;

Bind. Bind ::= "def" Ident "=" Expr ;
Sig. Sig ::= Ident ":" Type ;

-------------------------------------------------------------------------------
-- * Type
-------------------------------------------------------------------------------

TVar. Type2 ::= TVar ;
TApp. Type1 ::= Type1 Type2 ;
TFun. Type ::= Type1 "->" Type ;

MkTVar. TVar ::= Ident ;

coercions Type 2 ;

-------------------------------------------------------------------------------
-- * Pattern matching
-------------------------------------------------------------------------------

Branch. Branch ::= Pattern "=>" Expr ;
separator Branch ";" ;

PLit.   Pattern1 ::= Lit ;
PCatch. Pattern1 ::= "_" ;
PInj.   Pattern1 ::= Ident [Pattern1] ;

coercions Pattern 1 ;
separator Pattern1 " " ;

-------------------------------------------------------------------------------
-- * Expressions
-------------------------------------------------------------------------------

-- Let EPat act as variable and constructor
EPat. Expr2 ::= Ident ;
ELit. Expr2 ::= Lit ;
EApp. Expr1 ::= Expr1 Expr2 ;
ELet. Expr ::= "let" Ident "=" Expr "in" Expr1 ;
ELam. Expr ::= "\\" Ident "->" Expr ;
ECase. Expr1 ::= "case" Expr "of" "{" [Branch] "}" ;

coercions Expr 3 ;

-------------------------------------------------------------------------------
-- * Literals
-------------------------------------------------------------------------------

LInt.    Lit ::= Integer ;
LChar.   Lit ::= Char ;
LString. Lit ::= String ;
